# author='Giuseppe Vittucci and Artur Tarassow'
# email='giuseppe.vittucci@unimib.it'
# version='2.0'
# date='2016-10-28'

function matrix wooldridge_test_serial (series y "dependent variable",
                                        list X "regressors",
                                        int verbose[0:1:1] "Print details: 0=NO, 1=YES")
    # Wooldridge test for autocorrelation in panel data
    # Wooldridge (2002) Econometric Analysis of Cross Section and Panel Data, pp.282-283;
    # Giuseppe Vittucci (2011) and Artur Tarassow (2016).
    diff y X
    list dX = null
    loop foreach i X --quiet
        list dX += d_$i
    endloop
    ols d_y dX --robust --quiet
    matrix coef_stde = $coeff ~ $stderr
    string parnames = varname(dX)
    if verbose==1
        printf "\nFirst-Differenced equation (dependent d_y):\n"
        modprint coef_stde parnames
        printf "\nRegression of residuals of the FD equation on lagged residuals\n"
        printf "uhat = rho*uhat(-1) + e\n"
    endif
    series u = $uhat
    ols u u(-1) --robust --quiet
    matrix coef_stde = $coeff ~ $stderr
    scalar m = ($coeff[1,1]+0.5)^2/$vcv[1,1]
    scalar p = pvalue(F, 1, $df, m)
    if verbose==1
        string parnames = "uhat(-1)"
        modprint coef_stde parnames
        printf "\nWooldridge test for autocorrelation in panel data\nH0: no first-order autocorrelation (rho = -0.5)\n"
        printf "F(1,%g) = %g\n", $df, m
        printf "Prob > F = %g", p
    endif
    mat = $coeff ~ m ~ p
    colnames(mat,"ARcoeff Teststat. Pvalue")
    return mat
end function
