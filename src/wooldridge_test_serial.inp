# author='Giuseppe Vittucci and Artur Tarassow'
# email='giuseppe.vittucci@unimib.it'
# version='2.1'
# date='2019-07-11'

function matrix wooldridge_test_serial (series y "Dependent variable",
                                        list X "List of regressors",
                                        int verbose[0:1:1] "Print details: 0=NO, 1=YES")
    # Wooldridge test for autocorrelation in panel data
    # Wooldridge (2002) Econometric Analysis of Cross Section and
    # Panel Data, pp.282-283;
    # Giuseppe Vittucci (2011) and Artur Tarassow (2016).
    diff y X
    list dX = null
    loop foreach i X --quiet
        list dX += d_$i
    endloop
    ols d_y dX --robust --quiet
    matrix coef_stde = $coeff ~ $stderr
    string parnames = varname(dX)
    if verbose
        printf "\nFirst-Differenced equation (dependent d_%s):\n", argname(y)
        modprint coef_stde parnames
        printf "\nRegression of residuals of the first-difference equation on lagged residuals:\n"
        printf "uhat = rho*uhat(-1) + e\n"
    endif
    series u = $uhat
    ols u u(-1) --robust --quiet
    matrix coef_stde = $coeff ~ $stderr
    scalar m = ($coeff[1,1]+0.5)^2/$vcv[1,1]
    scalar p = pvalue(F, 1, $df, m)
    if verbose
        string parnames = "uhat(-1)"
        modprint coef_stde parnames
        printf "\nWooldridge test for autocorrelation in panel data.\n"
        printf "H0: no first-order autocorrelation (rho = -0.5):\n"
        printf "F(1,%g) = %g\n", $df, m
        printf "Prob > F = %g", p
    endif
    mat = $coeff ~ m ~ p
    cnameset(mat, "ARcoeff Teststat. P-value")
    return mat
end function


function void gui_wooldridge_test_serial (void)
    /* Wrapper function for gui menu-entry*/
    string yname = $model.depvar
    genseries("@yname", $model.ylist[1])
    matrix mat = wooldridge_test_serial(@yname, $model.xlist)
end function

